pragma.syntax("0.9")

def makeTestCase := <import:com.twistedmatrix.eunit.makeTestCase>
def makeBuilder := <import:com.twistedmatrix.ometa.makeBuilder>


def makeTestBuilder() {
    def self extends makeTestCase(self) {

        to test_makeGrammar() {
            def b := makeBuilder("foo")
            def wanted := e`def makeFoo(input) {
                              def foo extends makeRuntime(input, foo) {
                              }
                              return foo
                            }`
            self.assertEqual(b.makeGrammarSource([].asMap()), wanted)
        }

        to test_buildMethod() {
           def b := makeBuilder("foo")
           def wanted := e`def makeFoo(input) {
                              def foo extends makeRuntime(input, foo) {
                                  method rule_baz() {
                                      1 + 1
                                  }
                              }
                              return foo
                            }`
            self.assertEqual(b.makeGrammarSource(["baz" => e`1 + 1`]), wanted)
        }

        to test_apply() {
            def b := makeBuilder("foo")
            def wanted := e`foo._apply("baz", [one, two])`
            self.assertEqual(b.apply("baz", [e`one`, e`two`]),
                             wanted)
        }

        to test_exactly() {
            def b := makeBuilder("foo")
            def wanted := e`foo._exactly("x")`
            self.assertEqual(b.exactly("x"),
                             wanted)
        }

        to test_many() {
            def b := makeBuilder("foo")
            def wanted := e`def _G_many_1() { return x + y };
                            foo._many(_G_many_1)`
            self.assertEqual(b.many(e`x + y`), wanted)
        }

        to test_many1() {
            def b := makeBuilder("foo")
            def wanted := e`def _G_many1_1() { return x + y };
                            foo._many1(_G_many1_1)`
            self.assertEqual(b.many1(e`x + y`), wanted)
        }

        to test_optional() {
            def b := makeBuilder("foo")
            def wanted := e`def _G_or_1() { return 17 };
                            def _G_or_2() { return null };
                            foo._or([_G_or_1, _G_or_2])`
            self.assertEqual(b.optional(e`17`), wanted)
        }

        to test_or() {
            def b := makeBuilder("foo")
            def wanted := e`def _G_or_1() { return x + y };
                            def _G_or_2() { return a + b };
                            foo._or([_G_or_1, _G_or_2])`
            self.assertEqual(b.or([e`x + y`, e`a + b`]), wanted)
        }
    }
    return self
}
